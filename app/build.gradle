apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.savvasdalkitsis.betwixt"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            keyAlias KEYSTORE_ALIAS
            storePassword ""
            keyPassword ""
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':app:assembleRelease')) {
        // Only execute when we are trying to assemble a release build

        android.signingConfigs.release.storePassword =
                project.hasProperty("KEYSTORE_STORE_PASS") ? KEYSTORE_STORE_PASS : askFor("Please enter store passphrase: ")
        android.signingConfigs.release.keyPassword =
                project.hasProperty("KEYSTORE_KEY_PASS") ? KEYSTORE_KEY_PASS : askFor("Please enter key passphrase: ")
    }
}

def askFor(String msg) {
    def console = System.console()
    if (console) {
        return new String(console.readPassword("\n$msg"))
    } else {
        throw new GradleException('Could not find console in order to request password.\n' +
                'If you are running with a gradle daemon, try again with --no-daemon option')
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile ('com.android.support:appcompat-v7:23.2.0',
            'com.android.support:cardview-v7:23.2.0',
            'com.android.support:recyclerview-v7:23.2.0',
            'com.shazam.android:aspects:1.0.0@aar',
            'com.jakewharton:butterknife:7.0.1',
            'com.savvasdalkitsis:butterknife-aspects:1.0.0',
    )
    compile project(':betwixt')
}
