apply plugin: 'com.android.application'

android {
    compileSdkVersion sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.savvasdalkitsis.betwixt"
        minSdkVersion 14
        targetSdkVersion sdk_version
        versionCode 3
        versionName "1.2"
    }
    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            keyAlias KEYSTORE_ALIAS
            storePassword ""
            keyPassword ""
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':app:assembleRelease')) {
        // Only execute when we are trying to assemble a release build

        android.signingConfigs.release.storePassword =
                project.hasProperty("KEYSTORE_STORE_PASS") ? KEYSTORE_STORE_PASS : askFor("Please enter store passphrase: ")
        android.signingConfigs.release.keyPassword =
                project.hasProperty("KEYSTORE_KEY_PASS") ? KEYSTORE_KEY_PASS : askFor("Please enter key passphrase: ")
    }
}

static def askFor(String msg) {
    def console = System.console()
    if (console) {
        return new String(console.readPassword("\n$msg"))
    } else {
        throw new GradleException('Could not find console in order to request password.\n' +
                'If you are running with a gradle daemon, try again with --no-daemon option')
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation("com.android.support:appcompat-v7:$support_lib_version",
            "com.android.support:cardview-v7:$support_lib_version",
            "com.android.support:recyclerview-v7:$support_lib_version",
            'com.shazam.android:aspects:1.0.0@aar',
            "com.jakewharton:butterknife:$butterknife_version",
            'com.savvasdalkitsis:butterknife-aspects:1.0.0',
    )
    annotationProcessor "com.jakewharton:butterknife:$butterknife_version"
    implementation project(':betwixt')
    implementation project(':betwixt-rebound')
}
